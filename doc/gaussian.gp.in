# Plot Gauss curve with default parameters of Enfuse


load "@srcdir@/config.gp"


default_optimum = 0.5
default_width = 0.2

gaussian(y, y_opt, width) = exp(-0.5 * ((y - y_opt) / width)**2)


set samples 1023
set xlabel "Y"
set xtics 0.2
set ytics 0.2


set terminal unknown
plot [y = 0:1] \
     gaussian(y, default_optimum, 0.1) title "width = 0.1", \
     gaussian(y, default_optimum, 0.2) title "default width = 0.2", \
     gaussian(y, default_optimum, 0.4) title "width = 0.4"

set output "gaussian.txt"
set terminal dumb Dumb_Width Dumb_Height enhanced
replot

set output "gaussian.eps"
set terminal postscript eps enhanced
replot

set output "gaussian.svg"
set terminal svg size Svg_Width, Svg_Height dynamic enhanced
replot

# Newer Gnuplots have a "pdf" terminal.
set output "| ps2pdf -dEPSCrop - gaussian.pdf"
set terminal postscript eps size Pdf_Width, Pdf_Height enhanced
replot

set output "@RASTER_DIR@/gaussian.png"
set terminal png transparent size Png_Width, Png_Height enhanced
replot
