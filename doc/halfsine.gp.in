load "@srcdir@/config.gp"


default_optimum = 0.5
default_width = 0.2
fwhm_gaussian = 2.3548200450309493820231386529193992755
fwhm_halfsinusodial = 2.0943951023931954923084289221863352561


halfsine(y, y_opt, width) = \
        abs((y - y_opt) / (width * fwhm_gaussian / fwhm_halfsinusodial)) <= pi / 2.0 ? \
        cos((y - y_opt) / (width * fwhm_gaussian / fwhm_halfsinusodial)) : \
        0


set samples 1023
set xlabel "Y"
set xtics 0.2
set ytics 0.2


set terminal unknown
plot [y = 0:1] \
     halfsine(y, default_optimum, 0.1) title "width = 0.1", \
     halfsine(y, default_optimum, 0.2) title "default width = 0.2", \
     halfsine(y, default_optimum, 0.4) title "width = 0.4"

set output "halfsine.txt"
set terminal dumb Dumb_Width Dumb_Height enhanced
replot

set output "halfsine.eps"
set terminal postscript eps enhanced
replot

set output "halfsine.svg"
set terminal svg size Svg_Width, Svg_Height dynamic enhanced
replot

# Newer Gnuplots have a "pdf" terminal.
set output "| ps2pdf -dEPSCrop - halfsine.pdf"
set terminal postscript eps size Pdf_Width, Pdf_Height enhanced
replot

set output "@RASTER_DIR@/halfsine.png"
set terminal png transparent size Png_Width, Png_Height enhanced
replot
