# Show the "Log" transform we use to convert floating-point data
# before copying it into a pyramid.


load "@srcdir@/config.gp"


LogTransform(x) = x >= 0.0 ? 1.0 + log(1.0 + x) : 1.0 / (1.0 - x);


set grid
set key top left
set samples 1023
set xlabel "x"
set ylabel "x'"


set terminal unknown
plot [-20 : 100] LogTransform(x)

set output "log-transform.txt"
set terminal dumb Dumb_Width Dumb_Height enhanced
replot

set output "log-transform.eps"
set terminal postscript eps enhanced
replot

set output "log-transform.svg"
set terminal svg size Svg_Width, Svg_Height dynamic enhanced
replot

# Newer Gnuplots have a "pdf" terminal.
set output "| ps2pdf -dEPSCrop - log-transform.pdf"
set terminal postscript eps size Pdf_Width, Pdf_Height enhanced
replot

set output "@RASTER_DIR@/log-transform.png"
set terminal png transparent size Png_Width, Png_Height enhanced
replot
