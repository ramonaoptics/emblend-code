# Plot example of exposure-cutoff function

load "@srcdir@/config.gp"

Lower = 0.05
Upper = 0.97
Mu = 0.5
Sigma = 0.2

Gaussian(x, mu, sigma) = \
    exp(-0.5 * ((x - mu) / sigma)**2)

ExposureCutoff(Y, LowerCutoff, UpperCutoff) = \
    Y <= LowerCutoff ? 0.0 : (Y >= UpperCutoff ? 0.0 : Gaussian(Y, Mu, Sigma))

set grid
set key right bottom
set sample 1024
set xlabel "Y"
set xtics 0.2
#--set ylabel "weight"
set yrange [-0.1:1.1]
set ytics 0.2

set terminal unknown
plot [Y = 0:1] ExposureCutoff(Y, Lower, Upper)

set output "exposure-cutoff.txt"
set terminal dumb Dumb_Width Dumb_Height enhanced
replot

set output "exposure-cutoff.png"
set terminal png font FreeSans 10 size Png_Width, Png_Height enhanced
replot

set output "exposure-cutoff.eps"
set terminal postscript eps enhanced
replot

# Newer Gnuplots have a "pdf" terminal.
set output "| ps2pdf -dEPSCrop - exposure-cutoff.pdf"
set terminal postscript eps enhanced
replot
